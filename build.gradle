plugins {
    id "org.asciidoctor.convert" version "1.5.3"
    id "com.stehno.gradle.webpreview" version "0.2.0"
    id "com.stehno.gradle.site" version "0.0.3"
    id 'com.github.kt3k.coveralls' version '2.7.1'
    id 'groovy'
}

repositories {
    jcenter()
}

asciidoctor {
    options doctype: 'book'

    backends = ['html5']

    attributes 'source-highlighter': 'coderay',
        'coderay-linenums-mode': 'table',
        icon: 'font',
        linkattrs: true,
        encoding: 'utf-8'
}

tasks.site.dependsOn = ['build', 'vanilla-core:build', 'vanilla-core:groovydoc', 'vanilla-core:jacocoTestReport', 'asciidoctor']

task publishSite(type: GradleBuild, group: 'Publishing', description: 'Publishes the documentation web site.', dependsOn: ['site']) {
    buildFile = 'publish.gradle'
    tasks = ['publishGhPages']
}

site {
    siteUrl = 'http://stehno.com/vanilla'

    assetDir( 'vanilla-core/build/reports', into:'core', external:true )
    assetDir( 'vanilla-core/build/docs/groovydoc', into:'core/groovydoc', external: true)
}

coveralls {
    sourceDirs = subprojects.findAll().sourceSets.main.allSource.srcDirs.flatten()
}