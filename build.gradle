plugins {
  id "org.asciidoctor.convert" version "1.5.3"
  id "com.stehno.gradle.webpreview" version "0.1.1"
}

repositories {
    jcenter()
}

asciidoctor {
    options doctype: 'book'

    backends = ['html5']

    attributes 'source-highlighter'    : 'coderay',
               'coderay-linenums-mode' : 'table',
               icon                    : 'font',
               linkattrs               : true,
               encoding                : 'utf-8'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2'
}

task site(group: 'Documentation', description: 'Builds the documentation web site.', dependsOn: ['build', 'vanilla-core:build', 'vanilla-core:groovydoc', 'vanilla-core:jacocoTestReport', 'asciidoctor']) << {
    logger.info 'Building documentation web site...'

    // copy the templates
    copy {
        from 'src/site'
        into 'build/site'
        include '**/*.html'
        expand([ project_version:project.subprojects[0].version, year:Calendar.instance.get(Calendar.YEAR) ])
    }

    // copy the assets
    copy {
        from 'src/site'
        into 'build/site'
        include '**/css/**'
        include '**/fonts/**'
        include '**/js/**'
    }

    copy {
        from 'vanilla-core/build/docs'
        from 'vanilla-core/build/reports'
        include '**/**'
        into 'build/site'
    }

    copy {
        from 'build/asciidoc'
        include '**/**'
        into 'build/site/guide'
    }
}

task publishSite(type: GradleBuild, group: 'Publishing', description: 'Publishes the documentation web site.', dependsOn: ['site']) {
    buildFile = 'publish.gradle'
    tasks = ['publishGhPages']
}

task updateVersion(group: 'Documentation', description: 'Updates the version in documentation based on the project version') << {
    String newVersion = project.subprojects[0].version
    String oldVersion = project.property('from')

    logger.lifecycle "Updating documentation versions from ${oldVersion} to ${newVersion}..."

    ant.replace(file: 'README.md', token: oldVersion, value: newVersion)
    ant.replace(dir: 'src/docs/asciidoc', token: oldVersion, value: newVersion)
}

task checkVersion(group: 'Verification', description: 'Verify that the project version is reflected in the documentation.') << {
    logger.lifecycle "Verifying that the documentation references version ${project.subprojects[0].version}..."

    // Not the most efficient way to do this but should be ok for now
    boolean documented = ['README.md', 'src/docs/asciidoc/index.adoc'].every { f ->
        boolean ok = project.file(f).text.contains(project.subprojects[0].version)

        logger.info " - Checking: $f: $ok"
        ok
    }

    assert documented, 'The documented project version does not match the project version: Run "./gradlew updateVersion -Pfrom=OLD_VERSION" and try again.'
}

webPreview {
    resourceDir = file('build/site')
}
