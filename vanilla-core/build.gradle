plugins {
    id 'com.jfrog.bintray' version '1.5'
    id 'com.github.hierynomus.license' version '0.12.1'
}

task sourcesJar(type: Jar) {
    description = 'Creates sources JAR.'
    classifier = 'sources'

    from project.sourceSets.main.allSource
}

dependencies {
    compile 'com.mockrunner:mockrunner-jdbc:1.1.0'
}

artifacts {
    archives jar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourcesJar
        }
    }
}

license {
    header rootProject.file('license_header.txt')
    ext.name = 'Christopher J. Stehno'
    ext.email = 'chris@stehno.com'
    ext.year = Calendar.instance.get(Calendar.YEAR)
}

// NOTE: run bintrayUpload and then go to web and publish.
bintray {
    user = project.hasProperty('bintray-user') ? property('bintray-user') : null
    key = project.hasProperty('bintray-key') ? property('bintray-key') : null

    publications = ['mavenJava']

    pkg {
        repo = 'stehno'
        name = 'vanilla-core'
        desc = 'Reusable Groovy libraries.'
        licenses = ['Apache-2.0']
        labels = ['groovy']
    }
}
