plugins {
    id 'groovy'
    id 'maven-publish'
    id 'codenarc'
    id 'jacoco'
    id 'com.jfrog.bintray' version '1.6'
    id 'com.github.hierynomus.license' version '0.12.1'
}

version = '0.5.1'
group = 'com.stehno.vanilla'

sourceCompatibility = 1.8
targetCompatibility = 1.8
    
repositories {
    jcenter()
}

configurations {
    groovyDoc
}

dependencies {
    compile 'com.mockrunner:mockrunner-jdbc:1.1.0'
    
    compile "org.codehaus.groovy:groovy-all:$groovyVersion"

    testCompile 'junit:junit:4.12'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'

    groovyDoc "org.codehaus.groovy:groovy-groovydoc:$groovyVersion"
    groovyDoc "org.codehaus.groovy:groovy-ant:$groovyVersion"

    codenarc(
        "org.codenarc:CodeNarc:$codeNarcVersion",
        "org.codehaus.groovy:groovy-all:$groovyVersion"
    )    
}

groovydoc {
    groovyClasspath = configurations.groovyDoc
}

codenarc {
    toolVersion = codeNarcVersion
}

codenarcMain {
    ignoreFailures true
    configFile file('../config/codenarc-main.rules')

    maxPriority1Violations 0
    maxPriority2Violations 5
    maxPriority3Violations 10
}

codenarcTest {
    ignoreFailures true
    configFile file('../config/codenarc-test.rules')
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/reports/coverage"
    }
}

task sourcesJar(type: Jar) {
    description = 'Creates sources JAR.'
    classifier = 'sources'

    from project.sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourcesJar
        }
    }
}

license {
    header rootProject.file('config/license_header.txt')
    ext.name = 'Christopher J. Stehno'
    ext.email = 'chris@stehno.com'
    ext.year = Calendar.instance.get(Calendar.YEAR)

    excludes(['**/*.txt'])
}

// NOTE: run bintrayUpload and then go to web and publish.
bintray {
    user = bintrayUser
    key = bintrayKey

    publications = ['mavenJava']

    pkg {
        repo = 'stehno'
        name = 'vanilla-core'
        desc = 'Reusable Groovy libraries.'
        licenses = ['Apache-2.0']
        labels = ['groovy']
    }
}



